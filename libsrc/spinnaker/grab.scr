# For monitoring, we always want to display the newest buffer...
# This is different from recording where we want to write the oldest
# buffer first!

#max_warnings 3
max_warnings -1
< $macro_dir/system/mark_time.mac

Set cam1 Blackfly_S_BFS-U3-13Y3M
Set cam2 Flea3_FL3-U3-13Y3M

spinnaker
  advise "initializing..."
  init
  advise "DONE initializing..."
  list_cams
#  info Flea3_FL3-U3-13Y3M
#  select Flea3_FL3-U3-13Y3M
#  test
  quit

Define Release_Oldest 0
spinnaker capture release quit quit
.

Define Grab_Next 0
spinnaker capture grab quit quit
.

# at 170 fps, there is around 6msec between frames...

Set proc_usecs 3000

# this value causes 10% underrun...
#Set proc_usecs 6000
# this value causes 25% underrun...
#Set proc_usecs 7000
# this value causes 40% underrun...
#Set proc_usecs 8000
# this value causes 70% underrun...
#Set proc_usecs 10000
# this value causes 110% underrun...
#Set proc_usecs 12000

Define Init_Timestamp 0
Set last_ts -1
Set first_fid -1
.

Define Show_Timestamp 0
spinnaker
  chunks
    #display Timestamp cam$cam_idx.frame$newest
    fetch ts Timestamp cam$cam_idx.frame$newest
    #display FrameID cam$cam_idx.frame$newest
    fetch fid FrameID cam$cam_idx.frame$newest
    quit
  quit
If $last_ts>0 Show_Delta
Set last_ts $ts
If $first_fid<0 "Set first_fid $fid"
Add_Var fid -$first_fid
advise "Frame $fid"
.

Define Show_Delta 0
Assign delta ($ts-$last_ts)/1000000	# nano seconds?
advise "delta = $delta msec"
.

Define GrabEm 1 n
Init_Timestamp
advise "Grabbing $1 frames, starting capture"
spinnaker capture start quit quit
advise "capture started..."
Set i_grab 0
repeat $1
  advise "grabbing frame $i_grab..."
  do
    nop
    while !$image_ready
  Grab_Next
  Set image_ready 0
  #Show_Bytes cam$cam_idx.frame$newest v
  Show_Timestamp
  usleep $proc_usecs
  Release_Oldest
  Increment i_grab
  end
spinnaker capture stop quit quit
.

Define Time_It 2 macro_name arg
advise "Time_It $1 $2 BEGIN"
Zero_Time
$1 $2
Check_Time
advise "$total_ms milliseconds elapsed"
advise "Time_It DONE"
.

Define Show_Underrun_Count 0
spinnaker
  nodes
    select_map $$cam_name.stream_TL
#    info_all
    info StreamBufferUnderrunCount
    info StreamTotalBufferCount
    quit
  quit
.

Define Setup_Grab 2 cam n_buffers
spinnaker
  info $1
  nodes
    select_map $1.genicam
    info AcquisitionMode
    select_map $1.stream_TL
    info StreamBufferHandlingMode
    set StreamBufferHandlingMode NewestOnly
    info StreamBufferHandlingMode
    info StreamDefaultBufferCount
    set StreamDefaultBufferCount $2
    info StreamDefaultBufferCount
    quit
advise "selecting $1"
  select $1
  info $1
  capture
    advise "Setting up $2 buffers"
    set_n_buffers $2
    quit
  quit
.

Set cam_idx 1
Set cam_name cam$cam_idx

spinnaker
  select $$cam_name
  nodes
    select_map $$cam_name.genicam
    #info_all
    quit
  chunks
    enable Timestamp
    enable FrameID
    quit
  nodes
    select_map $$cam_name.genicam
    #info_all
    quit
  quit
Pause

# There seems to be some sort of internal bug that bites us when we increase the number
# of buffers???

Setup_Grab $$cam_name 10	# default, works
#Setup_Grab $$cam_name 11	# works
#Setup_Grab $$cam_name 12	# works
#Setup_Grab $$cam_name 13	# doesn't work
#Setup_Grab $$cam_name 15	# doesn't work

Set frm_idx 0
Set n_test 300
Time_It GrabEm $n_test
Show_Underrun_Count
Time_It GrabEm $n_test
Show_Underrun_Count
#Time_It GrabEm $n_test
#Show_Underrun_Count

Define Show_All_Nodes 0
spinnaker
  nodes
    select_map $$cam_name.device_TL
    info_all
    select_map $$cam_name.stream_TL
    info_all
    select_map $$cam_name.genicam
    info_all
    quit quit
.

exit


