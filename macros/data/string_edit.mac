Start_File string_edit.mac

# used to change spaces to underscores, for example...

Define Subst_Var 3 varname orig_char subst_char 
Insure_String_Obj tmpstring $$1

expressions
  read -
    tmpstring = tmpstring == $2 ? '$3' : tmpstring; #for spaces
    end
  quit

Get_String $1 tmpstring
.

Define Insure_String_Obj 2 obj_name string
# make sure the current data area is CPU...
# bug broken if string contains a quote???
If !obj_exists('$1') "Vector $1 1 strlen('$2')+1 string expressions export $1 quit"
If ncomps('$1')<=strlen('$2') "Delete_Image $1"
If !obj_exists('$1') "Vector $1 1 strlen('$2')+1 string expressions export $1 quit"
Set_String $1 $2
.

# these macros are useful when we don't have access to awk...
# But they were written having forgotten about the Explode macro!?

Define Get_First_Component 3 dest_varname source_string_obj delim
Get_String source_string $2
Assign source_idx strstr("$source_string","$3")
Substring tmp_substr $2 $source_idx 0
Display tmp_substr
Get_String $1 tmp_substr
Delete_Image tmp_substr
.

Define Strip_First_Component 3 new_obj_name source_string_obj delim
Get_String source_string $2
Assign source_len strlen("$source_string")
Assign delim_idx strstr("$source_string","$3")
# BUG check for valid delim_idx
Increment delim_idx
Substring $1 $2 $source_len-$delim_idx $delim_idx
.


#Set f gamma_fit.$dummy_user_username.1.asc		# We want to get $dummy_user_username and 1...  like awk...
# BUG?  make sure explode doesn't overflow if there are too many components or characters...

Define Explode 3 string char table
VSet $3 0
Set_String $3[0] $1
Assign l strlen('$1')
Assign x strstr('$1','$2')
Set i 0
Set n_string_components 1
If $x<0 exit_macro	# the separator does not appear
do
  If $i>=ncols('$3') "warn 'Explode:  too many components for table object $3!?' exit_macro"
  Subscalar sub_word $3[$i] $l-$x-1 $x+1
  Get_String sss sub_word
  Delete_Image sub_word
  VSet $3[$i][$x] 0	# add null terminator to this substring
  Increment n_string_components
  Set_String $3[$i+1] $sss
  Assign x strstr('$sss','$2')
  If $x==(-1) exit_macro
#  Print x
  Increment i
  #end
  while 1
.

Stop_File string_edit.mac

